plugins {
    id 'java'
    id 'application'
    id 'c'
    id 'org.jetbrains.kotlin.jvm'
}

group = 'com.dpforge'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    applicationDefaultJvmArgs = ["-Djava.library.path=" + file("${buildDir}/libs/doom/shared").absolutePath]
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

model {
    platforms {
        arm64 {
            architecture "arm64"
        }
    }

    components {
        doom(NativeLibrarySpec) {
            targetPlatform "arm64"
            binaries.all {
                def jvmHome = javaToolchains.launcherFor(java.toolchain).get().metadata.installationPath.asFile
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/darwin"
                    cCompiler.args '-mmacosx-version-min=10.9'
                    linker.args '-mmacosx-version-min=10.9'
                    linker.args '-stdlib=libc++'
                    linker.args '-L', "${projectDir}/libs"  // Add library search path
                    linker.args '-lmacosdoom'  // Link against libdoom.dylib (without 'lib' prefix)
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/linux"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${jvmHome}/include"
                    cCompiler.args "-I${jvmHome}/include/win32"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/freebsd"
                }
            }
        }
    }
}

classes.dependsOn 'doomSharedLibrary'